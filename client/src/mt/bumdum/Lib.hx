package mt.bumdum;

import common.MovieClip;
import pixi.core.display.DisplayObject;
import pixi.core.display.Container;
import pixi.filters.blur.BlurFilter;
import pixi.filters.extras.GlowFilter;

typedef Point = {x:Float, y:Float}

class En {
	inline static public function index(e:Dynamic) {
		return Type.enumIndex(e);
	}
	/*
		static public function get(e:Enum,idx:Int,?params:Array<Dynamic>) : Dynamic {
			return TypecreateEnum(e,Type.getEnumConstructs(e)[idx],params);
	}*/
}

class Num {
	static public function mm(a, b, c) {
		return Math.min(Math.max(a, b), c);
	}

	static public function sMod(n:Float, mod:Float) {
		if (mod == 0 || mod == null || n == null)
			return null;
		while (n >= mod)
			n -= mod;
		while (n < 0)
			n += mod;
		return n;
	}

	static public function hMod(n:Float, mod:Float) {
		if (mod == 0 || mod == null || n == null)
			return null;
		while (n > mod)
			n -= mod * 2;
		while (n < -mod)
			n += mod * 2;
		return n;
	}

	static public function rnd(n:Int, f:Float) {
		return Std.int(Math.pow(Math.random(), f) * n);
	}
}

class Geom {
	static public function getDist(o1:Point, o2:Point) {
		var dx = o1.x - o2.x;
		var dy = o1.y - o2.y;
		return Math.sqrt(dx * dx + dy * dy);
	}

	static public function getAng(o1:Point, o2:Point) {
		var dx = o1.x - o2.x;
		var dy = o1.y - o2.y;
		return Math.atan2(dy, dx);
	}

	static public function getParentCoord(mc:Container, parent:Container) {
		var par = null;
		var to = 0;
		var x:Float = mc.x;
		var y:Float = mc.y;
		while (true) {
			par = mc.parent;
			if (par.rotation != 0) {
				var dist = Math.sqrt(x * x + y * y);
				var a = Math.atan2(y, x);
				a += par.rotation * 0.0174;
				x = Math.cos(a) * dist;
				y = Math.sin(a) * dist;
			}

			x *= par.scale.x * 0.01;
			y *= par.scale.y * 0.01;

			x += par.x;
			y += par.y;

			if (par == parent || par == null) {
				return {x: x, y: y};
			}
			mc = par;
			if (to++ > 20) {
				trace("GET PARENT COORD ERROR");
				break;
			}
		}
		return null;
	}
}

class Col {
	static public function colToObj(col) {
		return {
			r: col >> 16,
			g: (col >> 8) & 0xFF,
			b: col & 0xFF
		};
	}

	static public function objToCol(o) {
		return (o.r << 16) | (o.g << 8) | o.b;
	}

	static public function colToObj32(col) {
		return {
			a: col >>> 24,
			r: (col >> 16) & 0xFF,
			g: (col >> 8) & 0xFF,
			b: col & 0xFF
		};
	}

	static public function objToCol32(o) {
		return (o.a << 24) | (o.r << 16) | (o.g << 8) | o.b;
	}

	static public function setPercentColor(mc:MovieClip, prc:Float, col, ?inc:Float, ?alpha = 100) {
		if (prc == 0) {
			mc.tint = 0xFFFFFF;
			return;
		}

		trace("FIXME");
		if (inc == null)
			inc = 0;
		var color = colToObj(col);
		var c = prc / 100;
		var ct = {_: null};
		var ct = {
			r: Std.int(c * color.r + inc),
			g: Std.int(c * color.g + inc),
			b: Std.int(c * color.b + inc),
		};
		setColor(mc, objToCol(ct));
	}

	static public function setColor(mc:MovieClip, col, ?dec) {
		mc.tint = col;
	}

	static public function mergeCol(col:Int, col2:Int, ?c) {
		if (c == null)
			c = 0.5;
		var o = Col.colToObj(col);
		var o2 = Col.colToObj(col2);
		var o3 = {
			r: Std.int(o.r * c + o2.r * (1 - c)),
			g: Std.int(o.g * c + o2.g * (1 - c)),
			b: Std.int(o.b * c + o2.b * (1 - c))
		}
		return Col.objToCol(o3);
	}

	static public function mergeCol32(col:Int, col2:Int, ?c) {
		if (c == null)
			c = 0.5;
		var o = Col.colToObj32(col);
		var o2 = Col.colToObj32(col2);
		var o3 = {
			r: Std.int(o.r * c + o2.r * (1 - c)),
			g: Std.int(o.g * c + o2.g * (1 - c)),
			b: Std.int(o.b * c + o2.b * (1 - c)),
			a: Std.int(o.a * c + o2.a * (1 - c))
		}
		return Col.objToCol32(o3);
	}

	static public function getRainbow(?c) {
		if (c == null)
			c = Math.random();
		var max = 3;
		var a:Array<Float> = [0.0, 0.0, 0.0];
		var part = (1 / max * 2);
		for (i in 0...max) {
			var med = part + i * 2 * part;
			var dif = Num.hMod(med - c, 0.5);
			a[i] = Math.min(1.5 - Math.abs(dif) * 3, 1);
		}
		return {
			r: Std.int(a[0] * 255),
			g: Std.int(a[1] * 255),
			b: Std.int(a[2] * 255)
		}
	}

	static public function shuffle(col:Int, inc:Int) {
		var o = colToObj(col);
		o.r = Std.int(Num.mm(0, o.r + (Math.random() * 2 - 1) * inc, 255));
		o.g = Std.int(Num.mm(0, o.g + (Math.random() * 2 - 1) * inc, 255));
		o.b = Std.int(Num.mm(0, o.b + (Math.random() * 2 - 1) * inc, 255));
		return objToCol(o);
	}

	static public function getWeb(col) {
		return "#" + StringTools.hex(col);
	}

	// WHITE IN YOUR BASE ------------------------------------ :)

	public static function rgb2Hex(r:Int, g:Int, b:Int, a:Bool = false) {
		if (!a)
			return (r << 16) + (g << 8) + b;
		var o = colToObj32((r << 16) + (g << 8) + b);
		o.a = 0xFF;
		return objToCol32(o);
	}

	public static function addAlpha(col:Int):Int {
		var o = colToObj32(col);
		o.a = 0xFF;
		return objToCol32(o);
	}

	public static function brighten(rgb:Int, percent:Int) {
		return mergeCol(rgb, 0xFFFFFF, percent / 100);
	}

	public static function darken(rgb:Int, percent:Int) {
		var col = colToObj(rgb);
		col.r -= Math.floor(col.r * percent / 100);
		col.g -= Math.floor(col.g * percent / 100);
		col.b -= Math.floor(col.b * percent / 100);
		return objToCol(col);
	}

	public static function cmyk2rbg(c:Int, m:Int, y:Int, k:Int):Int {
		// adapted from http://arcscripts.esri.com/details.asp?dbid=11276
		var r = 0;
		var g = 0;
		var b = 0;

		if (c + k > 100 || m + k > 100 || y + k > 100) {
			r = -99;
			g = -99;
			b = -99;

			var max = c > m ? c : m;
			max = max > y ? max : y;

			if (max == c)
				r = 0;
			if (max == m)
				g = 0;
			if (max == k)
				b = 0;

			var kk = 100 - max;
			if (r > 0 || r < 0)
				r = Math.round((1 - ((c + kk) / 100)) * 255);
			if (g > 0 || g < 0)
				g = Math.round((1 - ((m + kk) / 100)) * 255);
			if (b > 0 || b < 0)
				b = Math.round((1 - ((y + kk) / 100)) * 255);

			return objToCol({r: r, g: g, b: b});
		}

		r = Math.round((1 - ((c + k) / 100)) * 255);
		g = Math.round((1 - ((m + k) / 100)) * 255);
		b = Math.round((1 - ((y + k) / 100)) * 255);

		return objToCol({r: r, g: g, b: b});
	}
	/*
		static public function setColorMatrix(mc, m, dec){
			if(dec!=null){
				m = m.duplicate();
				for( i in 0...3 ){
					m[4+5*i] = dec;
				}
			}
			var fl = new flash.filters.ColorMatrixFilter();

			fl.matrix = m;
			mc.filters = [fl];
		}
	 */
}

class Str {
	static public function searchAndReplace(str:String, search:String, replace:String) {
		return str.split(search).join(replace);
	}
}

class Filt {
	static public function glow(mc:pixi.core.sprites.Sprite, distance = 2, strength:Float = 1, color = 0, inner = false) {
		var f = Type.createInstance(GlowFilter, [
			{
				distance: distance,
				outerStrength: inner ? 0 : strength,
				innerStrength: inner ? strength : 0,
				color: color
			}
		]);
		if (mc.filters == null) {
			mc.filters = [f];
			return;
		}

		mc.filters.push(f);
	}

	static public function blur(mc:pixi.core.sprites.Sprite, blurX:Float = 0, blurY:Float = 0) {
		var f = new BlurFilter();
		f.blurX = blurX;
		f.blurY = blurY;

		if (mc.filters == null) {
			mc.filters = [f];
			return;
		}

		mc.filters.push(f);
	}
}

class Tween {
	public var sx:Float;
	public var sy:Float;
	public var ex:Float;
	public var ey:Float;

	public function new(?sx:Float, ?sy:Float, ?ex:Float, ?ey:Float) {
		this.sx = sx;
		this.sy = sy;
		this.ex = ex;
		this.ey = ey;
	}

	public function getPos(c:Float) {
		return {
			x: sx * (1 - c) + ex * c,
			y: sy * (1 - c) + ey * c
		};
	}
}
